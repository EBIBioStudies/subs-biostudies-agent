buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE")
        classpath 'org.hidetake:gradle-ssh-plugin:1.1.3'
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
    }
}


group 'uk.ac.ebi.subs'
version '0.0.1-SNAPSHOT'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'application'
apply plugin: 'org.hidetake.ssh'

apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'
apply plugin: 'propdeps-eclipse'

mainClassName = "uk.ac.ebi.subs.BioStudiesAgentApp"

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.16.18'
    compile("uk.ac.ebi.subs:subs-processing-model:0.1.1-SNAPSHOT"){
        exclude group: 'org.springframework.boot', module :'spring-boot-starter-data-mongodb'
    }

    compile("uk.ac.ebi.subs:subs-data-model:0.2.0-SNAPSHOT"){
        exclude group: 'org.springframework.boot', module :'spring-boot-starter-data-mongodb'
    }
    compile("uk.ac.ebi.subs:subs-messaging:0.4.0-SNAPSHOT"){
        exclude group: 'org.springframework.boot', module :'spring-boot-starter-data-mongodb'
    }
    optional("org.springframework.boot:spring-boot-configuration-processor")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("commons-io:commons-io:2.5")

    compile ("uk.ac.ebi.subs:validator-common:0.1.0-SNAPSHOT") {
        exclude group: 'org.springframework.boot', module :'spring-boot-starter-data-mongodb'
    }
}

task ciTest(type: Test) {
    useJUnit {
        excludeCategories 'uk.ac.ebi.subs.BioStudiesApiDependentTest'
    }
    testLogging {
        exceptionFormat = 'full'
    }
}
configure(ciTest){
    group = 'verification'
    description = 'Run tests suitable for a continuous integration environment'
}


springBoot {
    executable = true
}

ssh.settings {
    user = 'sub_adm'
}

if (project.hasProperty('sshKeyFile')) {
    ssh.settings.identity = new File(sshKeyFile)
}

if (project.hasProperty('sshPassphrase')) {
    ssh.settings.passphrase = sshPassphrase
}

task printJarName {
    doLast {
        println "$jar.archivePath.name"
    }
}

task printJarFullPath {
    doLast {
        println "$jar.archivePath.absolutePath"
    }
}

remotes {
    staging {
        role('staging')
        host = 'mac-subs-004'
    }
}

task deployJar(type: SshTask, dependsOn: 'assemble') {
    doLast {
        def deployHome = "/data/" + project.name;
        File jarDeployDir = new File(deployHome,"jar")

        ssh.run {
            session(remotes.staging) {
                println "Creating $project.name directory in /data"
                execute "/data/create_dirs.sh $project.name", ignoreError: false
                println "Uploading jar $jar.archivePath.name to $jarDeployDir area on $remote"
                put(jar.archivePath.absolutePath, jarDeployDir.absolutePath)
                println "creating symbolic link to $jar.archiveName"
                def result = execute "ln -s -f $jarDeployDir/$jar.archiveName $jarDeployDir/$jar.baseName" + ".jar", ignoreError: true
                println result
                execute "chmod u+x $jarDeployDir/*", ignoreError: false
            }
        }
    }
    ssh.settings {
        knownHosts = allowAnyHosts
    }
}

compileJava.dependsOn(processResources)